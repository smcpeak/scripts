#!/usr/bin/perl -w
# re-do a given git merge cset

use strict;

if (@ARGV < 1) {
  die("usage: $0 [-interactive] [-quiet] <hash>\n");
}

# When true, allow user to merge files that have conflicts
# and enter a commit message, in order to create a merge
# node that might be pushed
my $interactive = 0;

# When true, suppress all output except the last message.
my $quiet = "";
my $quietDevNull = "";

while (scalar(@ARGV > 0) && $ARGV[0] =~ m/^-/) {
  my $arg = $ARGV[0];
  shift @ARGV;

  if ($arg eq "-interactive") {
    $interactive = 1;
  }
  elsif ($arg eq "-quiet") {
    $quiet = "--quiet";
    $quietDevNull = ">/dev/null 2>&1";     # some git commands are broken w.r.t. --quiet ....
  }
  else {
    die("unknown argument: $arg\n");
  }
}

if ($interactive && $quiet) {
  die("-interactive and -quiet are incompatible\n");
}

my $origMerge = $ARGV[0];
print("checking commit: $origMerge\n");

# Get the current HEAD.  That says which branch we are on, or if not
# on a branch, which revision we're at.  We need to restore that at
# the end.
my $origHEAD = `cat .git/HEAD`;       # should have a newline
print("current HEAD: $origHEAD");

# Get primary and merge parent.
my $parents = `git log -n 1 --format=format:%P $origMerge`;
if ($? != 0) {
  die("command failed: git log -n 1 --format=format:%P $origMerge\n");
}

my ($primaryParent, $mergeParent) =
  ($parents =~ m/^([0-9a-f]{40}) ([0-9a-f]{40})$/);
if (!defined($mergeParent)) {
  # Special-case non-merges so I don't have to filter elsewhere.
  if ($parents =~ m/^[0-9a-f]{40}$/) {
    print("$origMerge is not a merge node\n");
    exit(0);
  }

  die("unrecognized git log output: $parents");
}

diagnostic("primary parent: $primaryParent");
diagnostic("merge parent: $mergeParent");

# Check out primary parent in preparation for new merge.
run("git checkout $quiet $primaryParent");

# Merge in merge parent.
my $code = mysystemcode("git merge $mergeParent $quietDevNull");
if ($code > 1) {
  die("failed: git merge $mergeParent\n");
}

if ($code > 0) {
  # Manual merging required.
  if ($interactive) {
    # Start merging.
    run("git mergetool");

    # Throw away the .orig files.
    run("git ls-files --others --exclude-standard | xargs rm -f");

    # Commit it.
    run("git citool");
  }
  else {
    # Get set of conflicting files.
    my @conflictOutput = `git ls-files --unmerged`;
    if ($? != 0) {
      die("failed: git ls-files --unmerged\n");
    }
    my %conflictFiles = ();
    for my $line (@conflictOutput) {
      my ($fname) = ($line =~ m/\t(.*)$/);
      chomp($fname);
      $conflictFiles{$fname} = 1;
    }

    print("conflicting files:\n");
    for my $fname (sort(keys(%conflictFiles))) {
      print("  $fname\n");
    }

    # temporary file for stderr from "git show"
    my $tmpfile = "redo-git-merge.tmp.$$";

    # Assume that all the conflicting files should be merged according
    # to how they were merged originally.  This is not right for some
    # of our broken merges, but it allows the check for broken merges
    # to be mostly automatic, since broken merges have problems in the
    # files that didn't have conflicts.
    for my $fname (sort(keys(%conflictFiles))) {
      my $showCode = mysystemcode("git show '$origMerge:$fname' > '$fname' 2> $tmpfile");
      if ($showCode != 0) {
        # Check for case that file was deleted.
        my $errorMessage = `cat $tmpfile`;
        if (grep("exist", $errorMessage)) {
          print("looks like $fname was deleted in $origMerge, doing so here\n");
          run("git", "rm", $fname);
        }
        else {
          print("error from git show:\n");
          print($errorMessage);
          die("git show failed\n");
        }
      }
      else {
        # The -f argument is needed because we have some files, such as
        # analysis/cov-analyze/cmd-test-version-check/dir/c/emit/host/emit-db,
        # that are nominally filtered out by .gitignore but we have them
        # checked in anyway.
        run("git", "add", "-f", $fname);
      }
    }

    # Remove the temp file; but note that the next command would
    # do so as well.
    unlink($tmpfile);

    # Throw away the files that would be used by mergetool.
    #
    # EXCEPT: Leave behind log files, since the build scripts rely
    # on the ability to create a log file here so it can send email.
    run("git ls-files --others --exclude-standard | grep -v '\\.log\$' | xargs -d '\\n' rm -f");

    # --no-verify bypasses the pre-commit hooks, which can be
    # very slow.
    run("git commit $quiet --no-verify -m experimental-temporary-merge");
  }
}

# Get the new commit's hash.
my $newMerge = `git rev-parse HEAD`;
if ($? != 0) {
  die("failed: git rev-parse HEAD\n");
}
chomp($newMerge);
diagnostic("new merge: $newMerge");

# Diff with original merge.
$code = mysystemcode("git diff --name-only --exit-code $origMerge..$newMerge $quietDevNull");
if ($code > 1) {
  die("failed: git diff --name-only --exit-code $origMerge..$newMerge\n");
}

if ($code == 1) {
  print("$origMerge appears BROKEN\n");
}
else {
  print("$origMerge looks fine\n");
}

# Restore HEAD to where it was.
open(OUT, ">.git/HEAD") or die("failed to open .git/HEAD: $!\n");
print OUT ($origHEAD);
close(OUT) or die;

# Check out working files consistent with that HEAD.
mysystem("git reset --hard");

exit($code);


# run a command, and die if it fails
sub run {
  my (@cmd) = @_;

  my $code = mysystemcode(@cmd);
  if ($code >= 1) {
    die("exited with code $code: @cmd\n");
  }
}


# like mysystem, but return the exit code, or die if the
# child died by signal
sub mysystemcode {
  my (@cmd) = @_;

  my $code = mysystem(@cmd);
  if ($code != 0) {
    if ($code >= 256) {
      $code = $code >> 8;
      return $code;
    }
    else {
      die("died by signal $code: @cmd\n");
    }
  }
}


# like system, except bail if underlying command died by
# ctrl-c
sub mysystem {
  my (@cmd) = @_;

  diagnostic("@cmd");
  my $code = system(@cmd);
  if ($code == 2) {
    die("interrupted\n");
  }
  return $code;
}

sub diagnostic {
  if (!$quiet) {
    print(@_, "\n");
  }
}

# EOF
