#!/usr/bin/perl -w
# run gcov, but filter its output a bit
#
# This processes gcov output, filtering out lines that are not
# interesting for coverage purposes.  Specifically, these sorts of lines
# are not counted:
#
# * Lines containing "gcov-begin-ignore" and "gcov-end-ignore", and
#   all lines between such a pair.
#
# * Lines containing "gcov-ignore".
#
# * Lines matching any of the other regular expressions appearing in
#   the @ignoreRegexps list, below.

use strict 'subs';

@ignoreRegexps = (
  # Explicit directive to ignore coverage for one line.
  "gcov-ignore",

  # Functions that abort execution on paths that are "impossible".
  "xfailure",
  "xunimp",
  "abort\\s*\\(",
  "assert\\s*\\(\\s*!\\s*\"",       # assert(!"impossible")
  "av_failure",
  "av_abort",

  # Heuristically, if we are printing to stderr, we are probably
  # reporting some sort of unusual error.  I will ignore this too,
  # since most likely there is something else interesting in the
  # same code block if this should not be ignored.
  "fprintf\\s*\\(\\s*stderr\\s*,",

  # A comment placed on a line of code present only to silence a
  # compiler warning, but that is not actually executable.
  "silence warning",
  "[nN]ot reached",

  # Regexp of lines where it's irrelevant if executed.  These are
  # for lines that might appear to be not covered simply because
  # (for example) a macro expands to a 'break' statement that is
  # not reachable.  There will always be a more relevant statement
  # nearby whose coverage will be what I want to measure.
  "ASTCASE",
  "ASTNEXT",
  "ASTDEFAULT",
  "ASTENDCASE",

  # There is (was?) a bug in gcc/gcov where a "catch" statement is
  # always marked "######" even when the handler it guards *is*
  # executed.
  "catch\\s*\\(",

  # Similarly, GCC-9.3.0, sometimes flags uses of 'xassert' as
  # uncovered even though they are executed.
  #
  # Actually, that only happens if optimization is enabled, in which
  # case other FPs happen as well that I can't filter, so I'll assume
  # that optimization is disabled and remove this rule.
  #"xassert",

  # If the only thing on a line is `default:`, do not report it.  There
  # should be later statements that are more interesting.  Without this
  # rule, I get FPs when I put `xfailure` on the next line.
  "^\\s*default:\$",
);

sub usage {
  print("usage: $0 [options] filename.cc [file2.cc [...]]\n");
  print("\n");
  print("options:\n");
  print("  -q: only print stats for explicitly named filenames\n");
  print("  --object-directory dir: look for gcno/gcda files in 'dir'\n");
}

$quiet = 0;
$objectDirectory = "";

while (@ARGV > 0 && $ARGV[0] =~ m/^-/) {
  if ($ARGV[0] eq "-q") {
    $quiet++;
  }
  elsif ($ARGV[0] eq "--object-directory") {
    shift @ARGV;
    die if @ARGV == 0;
    $objectDirectory = $ARGV[0];
  }
  else {
    usage();
    die("unknown option: $ARGV[0]\n");
  }

  shift @ARGV;
}

if (@ARGV == 0) {
  usage();
  exit(0);
}

@filenames = @ARGV;


my @gcovCommand = ("gcov");
if ($objectDirectory ne "") {
  push @gcovCommand, ("--object-directory", $objectDirectory);
}
push @gcovCommand, @filenames;
print(join(" ", @gcovCommand), "\n");


$pid = open(KID, "-|");
if (!defined($pid)) {
  die("fork: $!\n");
}

if (!$pid) {  # child
  exec(@gcovCommand)
    || die("can't exec gcov: $!\n");
}

# read all of gcov's output before doing anything, because
# when it says "Creating ..." it is *not* finished creating
# that file, hence I will have a race condition if I try
# to do it online
@gcovOutput = <KID>;

if (!close(KID)) {
  # exit with same status
  $exit_value  = $? >> 8;
  $signal_num  = $? & 127;
  if ($exit_value) {
    exit($exit_value);
  }
  else {
    warn("gcov died with signal $signal_num\n");
    exit(10);
  }
}


# check for one condition early
@unexpected = grep {/^Unexpected/} @gcovOutput;
if (@unexpected) {
  print(join('\n', @unexpected));
  print("(perhaps you need to recompile?)\n");
  exit(4);
}


# if $execCount is the '#####' or '=====' marker, replace it with a zero;
# this is so I do not completely lose the count, but the marker
# is also not present (because it is a false alarm)
sub killHashes {
  my ($execCount) = @_;

  if ($execCount =~ m/\#/ || $execCount =~ m/=/) {
    # Replace with a right-aligned 0.
    return " " x (length($execCount)-1) . "0";
  }

  return $execCount;
}


# process the gcov output
foreach my $line (@gcovOutput) {
  if ($line =~ m/^Creating '(.*)'[\r]?$/) {
    my $fname = $1;
    print("Processing $fname\n");

    # read the whole thing in advance so I can write it anew
    if (!open(IN, "<", $fname)) {
      die("cannot read $fname: $!\n");
    }
    my @input = <IN>;
    close(IN) or die;

    # begin writing it
    if (!open(OUT, ">", $fname)) {
      die("cannot write $fname: $!\n");
    }

    my $lines = 0;           # total # of executable lines
    my $hit = 0;             # lines that were executed
    my $outputLineNum = 0;   # line number for complaining
    my $ignoring = 0;        # are we in a gcov-begin/end-ignore section?
    my $sourceLineNum = -1;  # last seen source line number
    my $ignoreExnLines = 0;  # whether to ignore exception-only lines

    foreach my $text (@input) {
      $outputLineNum++;
      chomp($text);

      if ($text =~ m/^---/ ||
          $text =~ m/^\S+:$/) {
        # These are lines showing additional detail about a line that
        # generates different functions, like a macro or a C++ template.
        # Ignore them; I will just look at the overall info, which
        # comes first.
        next;
      }

      # Parse the line.
      my ($pad1, $execCount, $unexecBB, $pad2, $codeLineNumber, $codeLine) =
        ($text =~ m/^(\s*)([0-9#=-]+)(\*?):(\s*)([0-9]+):(.*)$/);
      if (!defined($codeLine)) {
        die("mygcov: $fname:$outputLineNum: line not in expected format: \"$text\"\n");
      }
      chomp($codeLine);

      if ($codeLineNumber <= $sourceLineNum) {
        # We are still in the "additional detail" section; skip.
        next;
      }
      elsif ($codeLineNumber == $sourceLineNum+1) {
        # We have moved onto the next real line.
        $sourceLineNum = $codeLineNumber;
      }
      else {
        die("mygcov: $fname:$outputLineNum: was expecting to see info about ".
            "line " . ($sourceLineNum+1) . ": \"$text\"\n");
      }

      # ignoring?
      if ($ignoring) {
        if ($codeLine =~ m/gcov-end-ignore/) {
          $ignoring--;
        }
        $execCount = killHashes($execCount);
      }

      # begin ignoring?
      elsif ($codeLine =~ m/gcov-begin-ignore/) {
        $ignoring++;
        $execCount = killHashes($execCount);
      }

      # Ignore just this line?
      elsif (shouldIgnoreLine($codeLine)) {
        $execCount = killHashes($execCount);
      }

      # Disable complains in exception-only code?
      elsif ($codeLine =~ m/gcov-exception-lines-ignore/) {
        $ignoreExnLines = 1;
      }

      # Analyze $execCount.
      elsif ($execCount =~ m/\d/) {
        # executable and hit
        $lines++;
        $hit++;
      }
      elsif ($execCount =~ m/\#/) {
        # executable but not hit
        $lines++;
      }
      elsif ($execCount =~ m/=/) {
        # According to the documentation at
        # https://gcc.gnu.org/onlinedocs/gcc/Invoking-Gcov.html, a count
        # of "=====" means the line is not executed but is only
        # reachable as a result of exception handlers.
        if ($ignoreExnLines) {
          $execCount = killHashes($execCount);
        }
        else {
          $lines++;
        }
      }
      elsif ($execCount eq "-") {
        # line is not executable
      }
      else {
        warn("$fname:$outputLineNum: execution count not understood: $execCount\n");
      }

      # Print the line, possibly with modified $pad1 and $execCount.
      print OUT ($pad1, $execCount, $unexecBB, ":",
                 $pad2, $codeLineNumber, ":",
                 $codeLine, "\n");
    }

    close(OUT) or die;

    # print a report on the statistics for this file
    if (shouldShowReport($fname)) {
      my $pct = 0;
      if ($lines) {
        $pct = $hit / $lines * 100.0;
      }
      printf("%7.2f%s = %5d of %5d lines: %s\n",
             $pct, "%", $hit, $lines, $fname);
    }
  }

  # ignore other kind of output
}


# Return true if $codeLine matches any of the @ignoreRegexps.
sub shouldIgnoreLine {
  my ($codeLine) = @_;

  for my $regex (@ignoreRegexps) {
    if ($codeLine =~ m/$regex/) {
      return 1;
    }
  }

  return 0;
}


sub shouldShowReport {
  my ($fname) = @_;

  if ($quiet == 0) {
    return 1;
  }

  foreach $fn (@filenames) {
    if ($fname =~ m/$fn/) {
      return 1;
    }
  }

  return 0;
}


# EOF
