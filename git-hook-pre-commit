#!/usr/bin/perl -w
# My pre-commit hook.

# This file lives in ~/scripts.  Then I put into my git repos this
# .git/hooks/pre-commit file:
#
#   #!/bin/sh
#   exec perl $HOME/scripts/git-hook-pre-commit
#
# It does the following:
#
# * A frequent problem on Windows is I create a file using a native
# Windows program like Eclipse, which is interpreted by cygwin tools
# like git as being executable.  Then I get spuriously executable
# files in my repo.  This rejects the commit when that happens.

use strict;

my $verbose = 0;

while (scalar(@ARGV) > 0) {
  my $opt = $ARGV[0];
  shift @ARGV;

  if ($opt eq "-v") {
    $verbose = 1;
  }
  else {
    die("$0: unrecognized option: \"$opt\"\n");
  }
}

# Get list of files in commit.
if ($verbose) {
  print(    "git diff --cached --name-only\n");
}
my @files = `git diff --cached --name-only`;
if ($? != 0) {
  die("$0: git diff failed with exit code $?\n");
}

# Found any problem?
my $problems = 0;

# Process them.
foreach my $fname (@files) {
  chomp($fname);

  # Check for non-executable files.
  if (looksLikeNonExecutable($fname)) {
    # Get the permissions in the index of the staged file.
    if ($verbose) {
      print(   "git ls-files --stage '$fname'\n");
    }
    my $line = `git ls-files --stage '$fname'`;
    if ($? != 0) {
      die("$0: git ls-files --stage '$fname' failed with exit code $?\n");
    }
    chomp($line);
    if ($line eq "") {
      # If the file has been staged for deletion, ls-files prints nothing.
      next;
    }

    my ($perms) = ($line =~ m/^\d{3}(\d{3}) /);
    if (!defined($perms)) {
      die("$0: failed to parse output of git ls-files: $line\n");
    }

    # Reject if executable.
    if (hasExecuteBit($perms)) {
      print STDERR ("$fname: Looks like non-executable but is executable (as staged).\n");
      $problems++;
    }

    if (looksLikeSource($fname)) {
      inspectSourceContents($fname);
    }
  }
}

print STDERR ("$0 found $problems problems.\n");
exit($problems? 2 : 0);


# Return true if $fname looks like source code based on its file name.
sub looksLikeSource {
  my ($fname) = @_;

  if ($fname =~ m/\.(c|h|cc|hh|cpp|hpp|cxx|hxx|C|H)$/) {
    return 1;
  }
  else {
    return 0;
  }
}


# Return true if $fname looks like a non-executable based on its file name.
sub looksLikeNonExecutable {
  my ($fname) = @_;

  if (looksLikeSource($fname)) {
    return 1;
  }

  if ($fname =~ m/\.(txt|html)$/) {
    return 1;
  }

  if ($fname =~ m/$(README|readme)/) {
    return 1;
  }

  return 0;
}


# Return true if $perms, a string of 3 octal digits interpreted as a
# unix permissions bit set, has any of the execute (LSB) bits set.
sub hasExecuteBit {
  my ($perms) = @_;

  if ($perms =~ m/[1357]/) {
    return 1;
  }
  else {
    return 0;
  }
}


# Check the contents of source code for prohibited things.
sub inspectSourceContents {
  my ($fname) = @_;

  open(IN, "<$fname") or die("Cannot read $fname: $!\n");
  my $lineNumber = 0;
  my $line;
  while (defined($line = <IN>)) {
    $lineNumber++;

    # Check for NOCHECKIN.
    if ($line =~ m/NOCHECKIN/) {
      print STDERR ("$fname:$lineNumber: found NOCHECKIN\n");
      $problems++;
    }
  }
  close(IN) or die;
}


# EOF
